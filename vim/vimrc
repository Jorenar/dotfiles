" # VIMRC #

" BASICS {{{1

filetype plugin indent on
set backspace=indent,eol,start
set nocompatible
syntax enable

" XDG support    (`set rtp^=$VIMDOTDIR` is in $VIMINIT)
set backupdir=$XDG_DATA_HOME/vim/backup
set directory=$XDG_CACHE_HOME/vim/swap
set packpath^=$XDG_DATA_HOME/vim
set runtimepath+=$VIMDOTDIR/after
set runtimepath+=$XDG_DATA_HOME/vim
set undodir=$XDG_CACHE_HOME/vim/undo

if !has('nvim') " Neovim has its own special location
  set viminfo+=n$XDG_CACHE_HOME/vim/viminfo
endif

" "Force" Python3 (helpful for plugins)
if has('python3')
  python3 #
endif

silent! helptags ALL " genereate help tags

" COLORSCHEME {{{1
try
  colorscheme darkness
catch
  set bg=dark
  hi! ColorColumn ctermbg=233
  hi! Comment     ctermfg=240
  hi! link Folded Comment
  hi! link LineNr Comment
endtry

" COMMANDS {{{1

command! -nargs=* GrepRename call <SID>GrepRename(<f-args>)
command! -nargs=* TermDebug normal :Termdebug <args><CR><C-w>K<C-w>j<C-w>L
command! -nargs=+ FillLine call <SID>FillLine(<f-args>)
command! -nargs=+ Grep execute "lvimgrep /".<q-args>."/j **"
command! -nargs=? -bang Spelling setlocal spell<bang> spelllang=<args>
command! -range -nargs=+ Align <line1>,<line2>!column -Lts'<args>' -o'<args>'
command! -range -nargs=0 -bang VisSort sil! keepj <line1>,<line2>call <SID>VisSort(<bang>0)
command! -range=% Sort normal :<line1>,<line2>sort i<CR>
command! Book setlocal nonumber wrap columns=90 colorcolumn= laststatus=0
command! ExecCurrentLine normal :.w !sh<CR>
command! SortBlock :normal! vip:sort i<CR>
command! SudoW execute 'silent! write !sudo tee % >/dev/null' | edit!
command! Term vertical terminal
command! Todo lvimgrep "\<TODO\>" %

" FORMATTING {{{1

augroup FORMATOPTIONS
  autocmd!
  autocmd BufWinEnter * set fo-=c fo-=r fo-=o " Disable continuation of comments to the next line
  autocmd BufWinEnter * set formatoptions+=j  " Remove a comment leader when joining lines
  autocmd BufWinEnter * set formatoptions+=l  " Don't break a line after a one-letter word
  autocmd BufWinEnter * set formatoptions+=n  " Recognize numbered lists
  autocmd BufWinEnter * set formatoptions-=q  " Don't format comments
  autocmd BufWinEnter * set formatoptions-=t  " Don't autowrap text using 'textwidth'
augroup END

set cindent
set expandtab
set shiftround
set shiftwidth=0   " If 0, then uses value of 'tabstop'
set softtabstop=-1 " If negative, then uses 'shiftwidth' (which can use 'tabstop')
set tabstop=4
set textwidth=79

" For thise filetypes set 'tabstop' to 2
execute "autocmd filetype ".
      \   "html,xhtml,xml"    .",".
      \   "*css"              .",".
      \   "javascript,json"   .",".
      \   "cmake"             .",".
      \   "lua"               .",".
      \   "markdown"          .",".
      \   "vim"               .",".
      \ " setlocal tabstop=2"

" For thise filetypes set 'tabstop' to 3
execute "autocmd filetype ".
      \   "ada"                .",".
      \   "plaintex,tex"       .",".
      \ " setlocal tabstop=3"

let s:formatprg_for_filetype = {
      \ "c"          : "uncrustify --l C base kr mb",
      \ "cpp"        : "uncrustify --l CPP base kr mb stroustrup",
      \ "cmake"      : "cmake-format --command-case lower -",
      \ "css"        : "css-beautify -s 2 --space-around-combinator",
      \ "go"         : "gofmt",
      \ "html"       : "tidy -q -w -i --show-warnings 0 --show-errors 0 --tidy-mark no",
      \ "java"       : "uncrustify --l JAVA base kr mb java",
      \ "javascript" : "js-beautify -s 2",
      \ "json"       : "js-beautify -s 2",
      \ "python"     : "autopep8 -",
      \ "sql"        : "sqlformat -k upper -r -",
      \ "xhtml"      : "tidy -asxhtml -q -m -w -i --show-warnings 0 --show-errors 0 --tidy-mark no --doctype loose",
      \ "xml"        : "tidy -xml -q -m -w -i --show-warnings 0 --show-errors 0 --tidy-mark no",
      \}

for [ft, fp] in items(s:formatprg_for_filetype)
  execute "autocmd filetype ".ft." let &l:formatprg=\"".fp."\" | setlocal formatexpr="
endfor

" FUNCTIONS {{{1
" GrepRename - replace through whole project {{{2
function! s:GrepRename(expr1, expr2) abort
  execute "tabe | lvimgrep /\\C\\W".a:expr1."\\W/j ** | ldo s/\\C\\\(\\W\\)".a:expr1."\\\(\\W\\)/\\1".a:expr2."\\2/gc | update" | q
endfunction

" FillLine - fill line with characters to given column {{{2
function! s:FillLine(str, ...) abort
  let to_column = get(a:, 1, &tw)
  let reps = (to_column - col("$")) / len(a:str)
  if reps > 0
    .s/$/\=(' '.repeat(a:str, reps))/
  endif
endfunction

" VisSort - sorts based on visual-block selected portion of the lines {{{2
function! s:VisSort(isnmbr) range abort
  if visualmode() != "\<c-v>"
    execute "silent! ".a:firstline.",".a:lastline."sort i"
    return
  endif
  let firstline = line("'<")
  let lastline  = line("'>")
  let keeprega  = @a
  silent normal! gv"ay
  '<,'>s/^/@@@/
  silent! keepjumps normal! '<0"aP
  if a:isnmbr
    silent! '<,'>s/^\s\+/\=substitute(submatch(0),' ','0','g')/
  endif
  execute "sil! keepj '<,'>sort i"
  execute "sil! keepj ".firstline.",".lastline.'s/^.\{-}@@@//'
  let @a = keeprega
endfun

" VSetSearch - Search for selected text, forwards or backwards. {{{2
function! s:VSetSearch(cmdtype) abort
  let temp = @s
  norm! gv"sy
  let @/ = '\V' . substitute(escape(@s, a:cmdtype.'\'), '\n', '\\n', 'g')
  let @s = temp
endfunction

" FOR STATUS LINE {{{2
" FileEncoding {{{3
function! FileEncoding() abort
  return (&fenc == "" ? &enc : &fenc).((exists("+bomb") && &bomb) ? " BOM" : "")
endfunction

" FileSize {{{3
function! FileSize() abort
  let bytes = getfsize(expand(@%))
  if (bytes >= 1024*1024)
    return printf('~%.1f MiB', bytes/(1024*1024.0))
  elseif (bytes >= 1024)
    return printf('~%.1f KiB', bytes/1024.0)
  elseif (bytes <= 0)
    return '0 B'
  else
    return bytes . ' B'
  endif
endfunction

" IssuesCount {{{3
function! IssuesCount() abort
  if get(g:, 'ale_enabled', 0)
    let l:counts = ale#statusline#Count(bufnr(''))
    let l:all_errors = l:counts.error + l:counts.style_error
    let l:all_non_errors = l:counts.total - l:all_errors
    return "*".all_errors."E ".all_non_errors."w*"
  else
    return "*".len(filter(getqflist(), 'v:val.valid'))." issues*"
  endif
endfunction

" ModifBufs {{{3
function! ModifBufs() abort
  let cnt = len(filter(getbufinfo(), 'v:val.changed'))
  return cnt == 0 ? "" : ( &mod ? "[+". (cnt>1?cnt:"") ."]" : "[".cnt."]" )
endfunction

" Number of buffers {{{3
function! NumOfBufs() abort
  let num = len(getbufinfo({'buflisted':1}))
  let hid = len(filter(getbufinfo({'buflisted':1}), 'empty(v:val.windows)'))
  return hid ? num-hid."+".hid : num
endfunction

" MAPPINGS {{{1
" Normal mode {{{2
nnoremap ' `
nnoremap '' ``
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l
nnoremap <expr> <F4> &virtualedit == "all" ? ":set virtualedit=\<CR>" : ":set virtualedit=all\<CR>"
nnoremap <F3> :set cursorline! cursorcolumn!<CR>
nnoremap <Leader><F5> :syntax off <bar> syntax on<CR>
nnoremap <Leader>= gg=G``
nnoremap <Leader>b :bnext<CR>
nnoremap <Leader>h :nohlsearch<CR>
nnoremap <Leader>q m"gggqG`"
nnoremap <Leader>r :%s/\<<C-r><C-w>\>//g<Left><Left>
nnoremap <Leader>R :%s/\<<C-r><C-w>\>/<C-r><C-w>/g<Left><Left>
nnoremap <M-S-a> :copen<CR>
nnoremap <silent> <expr> <F5> g:colors_name == "darkness" ? ":syntax reset <bar> let g:colors_name=''\<CR>" : ":colo darkness\<CR>"
nnoremap gx :call job_start('xdg-open '.expand("<cfile>"))<CR>
nnoremap j gj
nnoremap k gk

" Insert mode {{{2
inoremap <expr> <C-o> pumvisible() ? "\<C-n>" : "\<C-o>"

" Terminal mode {{{2
tnoremap <C-h> <C-\><C-n><C-w>h
tnoremap <C-j> <C-\><C-n><C-w>j
tnoremap <C-k> <C-\><C-n><C-w>k
tnoremap <C-l> <C-\><C-n><C-w>l
tnoremap <silent> <C-\><C-\> <C-\><C-n>
tnoremap <silent> <C-\><C-m> <C-\><C-n>:let b:auto_insert = 1 - b:auto_insert<CR>

" Visual mode {{{2
xnoremap # :<C-u>call <SID>VSetSearch('?')<CR>?<C-r>=@/<CR><CR>
xnoremap * :<C-u>call <SID>VSetSearch('/')<CR>/<C-r>=@/<CR><CR>
vnoremap <Leader>s y:%s//g<Left><Left><C-r>"/

" ### DISABLE {{{2
map gh <nop>
vmap s <nop>

" OPTIONS {{{1

set backup
set colorcolumn=+1,+21
set hidden
set hlsearch
set ignorecase
set incsearch
set laststatus=2
set linebreak
set modeline
set nomodelineexpr
set nowrap
set number
set scrolloff=5
set sessionoptions=blank,buffers,folds,tabpages,winsize
set shortmess+=I
set showcmd
set smartcase
set splitbelow
set splitright
set switchbuf=useopen
set title
set undofile
set wildmenu
set wildmode=full

set completeopt=menuone,noinsert,noselect
set omnifunc=syntaxcomplete#Complete
set pumheight=20

if has('nvim')
  set guicursor=n-v-c-sm-i-r-ci-ve-cr-o:block " setting a:block breaks blinking
  set wildoptions-=pum " don't use popupmenu for wildmode completion
endif

set foldmethod=indent
set nofoldenable

set dictionary+=/usr/share/dict/polish
set dictionary+=/usr/share/dict/words
set path=**,./
set tags+=.git/tags;

" PLUGINS AND PACKAGES {{{1

if empty(glob(substitute(&packpath, ",.*", "", "")."/pack/plugins/opt/minPlug")) " {{{
  call system("git clone --depth=1 https://github.com/Jorengarenar/minPlug ".
        \ substitute(&packpath, ",.*", "", "")."/pack/plugins/opt/minPlug")
  autocmd VimEnter * silent! MinPlugInstall | echo "minPlug: INSTALLED"
endif " }}}

packadd minPlug

MinPlug artur-shaik/vim-javacomplete2
MinPlug ctrlpvim/ctrlp.vim
MinPlug dense-analysis/ale
MinPlug Jorengarenar/fauxClip
MinPlug Jorengarenar/miniSnip
MinPlug Jorengarenar/vim-darkness
MinPlug majutsushi/tagbar
MinPlug mbbill/undotree
MinPlug tpope/vim-surround

packadd cfilter
packadd matchit
packadd termdebug
source  $VIMRUNTIME/ftplugin/man.vim

" MAPPINGS
nmap <M-a> <Plug>(ale_detail)
nmap s ys
nnoremap <F1> :UndotreeToggle<cr>
nnoremap <F2> :ALEToggle<CR>
nnoremap <Leader>t :Tagbar<CR>
vmap s S

" OPTIONS
let g:ale_echo_msg_format         = '[%linter%]: %s'
let g:ale_enabled                 = 0
let g:ale_set_loclist             = 0
let g:ale_set_quickfix            = 1
let g:ale_set_signs               = 0
let g:ctrlp_brief_prompt          = 1
let g:ctrlp_clear_cache_on_exit   = 0
let g:ctrlp_extensions            = [ 'buffertag', 'quickfix' ]
let g:ctrlp_prompt_mappings       = { 'PrtExit()': [ '<C-[>', '<Esc>', '<C-c>' ] }
let g:ctrlp_types                 = [ 'fil' ]
let g:tagbar_compact              = 1
let g:tagbar_sort                 = 0
let g:undotree_SetFocusWhenToggle = 1

let g:miniSnip_extends = {
      \ "cpp"      : [ "c" ],
      \ "markdown" : [ "html" ],
      \ "tex"      : [ "plaintex" ],
      \ }

hi! link TagbarNestedKind Comment
hi! link TagbarType Comment

" STATUS LINE {{{1

set statusline=
set statusline+={%{NumOfBufs()}}       " Number of buffers
set statusline+=\ \                    " Separator
set statusline+=%f                     " Relative path to the file
set statusline+=\ \                    " Separator
set statusline+=%y                     " Filetype
set statusline+=[%{&ff}]               " File format
set statusline+=[%{FileEncoding()}]    " File encoding
set statusline+=\ \                    " Separator
set statusline+=[%{&fo}]               " Format options
set statusline+=\ \                    " Separator
set statusline+=[%{FileSize()}]        " File size
set statusline+=\ \                    " Separator
set statusline+=%r                     " Readonly flag
set statusline+=%{&ma\|\|&ro?'':'[-]'} " No modifiable flag
set statusline+=%w                     " Preview flag
set statusline+=\ \                    " Separator
set statusline+=%{ModifBufs()}         " Modified flag (extended)
set statusline+=%=                     " Switch to the right side
set statusline+=%{IssuesCount()}       " Count of errors, warnings
set statusline+=\ \                    " Separator
set statusline+=<0x%02B>               " Current char hex code
set statusline+=\ \                    " Separator
set statusline+=%l/                    " Current line
set statusline+=%L                     " Total lines
set statusline+=\ \:\ %c\              " Current column

" ### OTHER {{{1

augroup OTHER " {{{
  autocmd!

  " Open file at the last known position
  autocmd BufReadPost * if line("'\"") >= 1 && line("'\"") <= line("$") && &ft !~# 'commit' | exe "normal! g`\"" | endif

  " Trim trailing whitespace
  autocmd BufWritePre * silent! undojoin | %s/\s\+$//e | %s/\(\n\r\?\)\+\%$//e

  " Set 'foldmethod' to 'syntax'
  autocmd filetype css,javascript,json,typescript setlocal foldmethod=syntax

  " Protect folds when toggling insert mode / switching windows
  autocmd InsertEnter * if !exists('w:last_fdm') | let w:last_fdm=&foldmethod | setlocal foldmethod=manual | endif
  autocmd InsertLeave,WinLeave * if exists('w:last_fdm') | let &l:foldmethod=w:last_fdm | unlet w:last_fdm | endif

  " Automatically open QuickFix
  autocmd QuickFixCmdPost [^l]* nested cwindow
  autocmd QuickFixCmdPost    l* nested lwindow

  " Terminal options
  execute 'autocmd '.(has("nvim") ? "TermOpen" : "TerminalOpen").' *
        \ let b:auto_insert = 1 | setlocal nonumber |
        \ autocmd WinEnter <buffer> if b:auto_insert | silent! execute "normal i" | endif'

augroup END " }}}

call mkdir(&backupdir, 'p')
call mkdir($XDG_DATA_HOME."/vim/spell", 'p')

let g:sql_type_default = 'mysql'
let javaScript_fold    = 1

" vim: fdm=marker fen:
