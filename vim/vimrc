" # VIMRC #

" BASICS {{{1

filetype plugin indent on
set backspace=indent,eol,start
set nocompatible
syntax enable

" XDG support    (`set rtp^=$VIMDOTDIR` is in $VIMINIT)
set backupdir=$XDG_DATA_HOME/vim/backup
set directory=$XDG_CACHE_HOME/vim/swap
set packpath^=$XDG_DATA_HOME/vim
set runtimepath+=$VIMDOTDIR/after
set runtimepath+=$XDG_DATA_HOME/vim
set undodir=$XDG_CACHE_HOME/vim/undo
set viminfo+=n$XDG_CACHE_HOME/vim/viminfo

" COLORSCHEME {{{1
try
  colorscheme darkness
catch
  set bg=dark
  hi! ColorColumn ctermbg=233
  hi! Comment     ctermfg=240
  hi! link Folded Comment
  hi! link LineNr Comment
endtry

" COMMANDS {{{1

command! -nargs=* GrepRename call <SID>GrepRename(<f-args>)
command! -nargs=* TermDebug normal :Termdebug <args><CR><C-w>K<C-w>j<C-w>L
command! -nargs=+ FillLine call <SID>FillLine(<f-args>)
command! -nargs=+ Grep execute "lvimgrep /".<q-args>."/j **"
command! -nargs=? -bang Spelling setlocal spell<bang> spelllang=<args>
command! -range -nargs=+ Align <line1>,<line2>!column -Lts'<args>' -o'<args>'
command! -range -nargs=0 -bang VisSort sil! keepj <line1>,<line2>call <SID>VisSort(<bang>0)
command! -range=% Sort normal :<line1>,<line2>sort i<CR>
command! ExecCurrentLine normal :.w !sh<CR>
command! SortBlock :normal! vip:sort i<CR>
command! SudoW execute 'silent! write !sudo tee % >/dev/null' | edit!
command! Term vertical terminal

" FORMATTING {{{1

augroup FORMATOPTIONS
  autocmd!
  autocmd BufWinEnter * set fo-=c fo-=r fo-=o " Disable continuation of comments to the next line
  autocmd BufWinEnter * set formatoptions+=j  " Remove a comment leader when joining lines
  autocmd BufWinEnter * set formatoptions+=l  " Don't break a line after a one-letter word
  autocmd BufWinEnter * set formatoptions+=n  " Recognize numbered lists
  autocmd BufWinEnter * set formatoptions-=q  " Don't format comments
  autocmd BufWinEnter * set formatoptions-=t  " Don't autowrap text using 'textwidth'
augroup END

let g:html_indent_autotags = "html"
let g:html_indent_style1   = "inc"
set cindent
set expandtab
set shiftround
set shiftwidth=0   " If 0, then uses value of 'tabstop'
set softtabstop=-1 " If negative, then uses 'shiftwidth' (which can use 'tabstop')
set tabstop=4
set textwidth=79

" For thise filetypes set 'tabstop' to 2
execute "autocmd filetype ".
      \   "html,css,xhtml,xml" .",".
      \   "javascript"         .",".
      \   "lua"                .",".
      \   "markdown"           .",".
      \   "vim"                .",".
      \ " setlocal tabstop=2"

let s:formatprg_for_filetype = {
      \ "c"          : "astyle --style=kr -s4 -S -xU -k1 -O -c -H -xW -j",
      \ "cpp"        : "astyle --style=stroustrup -s4 -S -xU -k1 -O -c -H -xW -j -xG -xV",
      \ "css"        : "css-beautify -s 2 --space-around-combinator",
      \ "go"         : "gofmt",
      \ "html"       : "tidy -q -w -i --show-warnings 0 --show-errors 0 --tidy-mark no",
      \ "java"       : "astyle --style=java -s4 -S -xU -k1 -O -c -H -xW -j",
      \ "javascript" : "js-beautify -s 2",
      \ "json"       : "js-beautify",
      \ "python"     : "autopep8 -",
      \ "sql"        : "sqlformat -k upper -r -",
      \ "xhtml"      : "tidy -asxhtml -q -m -w -i --show-warnings 0 --show-errors 0 --tidy-mark no --doctype loose",
      \ "xml"        : "tidy -xml -q -m -w -i --show-warnings 0 --show-errors 0 --tidy-mark no",
      \}

for [ft, fp] in items(s:formatprg_for_filetype)
  execute "autocmd filetype ".ft." let &l:formatprg=\"".fp."\" | setlocal formatexpr="
endfor

" FUNCTIONS {{{1
" ALE toggle highlight {{{2
function! ALE_toggle_highlight()
  if matchstr(execute('hi ALEError'), 'cleared') == "cleared"
    highlight link ALEError   ErrorMsg
    highlight link ALEWarning WarningMsg
  else
    highlight ALEError   cterm=NONE
    highlight ALEWarning cterm=NONE
  endif
endfunction

" GrepRename - replace through whole project {{{2
function! s:GrepRename(expr1, expr2) abort
  execute "tabe | lvimgrep /\\C\\W".a:expr1."\\W/j ** | ldo s/\\C\\\(\\W\\)".a:expr1."\\\(\\W\\)/\\1".a:expr2."\\2/gc | update" | q
endfunction

" FillLine - fill line with characters to given column {{{2
function! s:FillLine(str, ...) abort
  let to_column = get(a:, 1, &tw)
  let reps = (to_column - col("$")) / len(a:str)
  if reps > 0
    .s/$/\=(' '.repeat(a:str, reps))/
  endif
endfunction

" UltiSnips Completion {{{2
function! UltiSnips_complete(...) abort
  let l:word = matchstr(getline('.'), '\S\+\%'.col('.').'c')
  if !empty(UltiSnips#SnippetsInCurrentScope(1))
    let l:suggestions = map(filter(keys(g:current_ulti_dict_info), 'stridx(v:val, l:word) == 0'),
          \  '{
          \      "word": v:val,
          \      "menu": "  ".get(g:current_ulti_dict_info[v:val], "description", ""),
          \      "dup" : 1
          \   }')
    call complete(col('.') - len(l:word), l:suggestions)
  endif
  return ''
endfunction

" VisSort - sorts based on visual-block selected portion of the lines {{{2
function! s:VisSort(isnmbr) range abort
  if visualmode() != "\<c-v>"
    execute "silent! ".a:firstline.",".a:lastline."sort i"
    return
  endif
  let firstline = line("'<")
  let lastline  = line("'>")
  let keeprega  = @a
  silent normal! gv"ay
  '<,'>s/^/@@@/
  silent! keepjumps normal! '<0"aP
  if a:isnmbr
    silent! '<,'>s/^\s\+/\=substitute(submatch(0),' ','0','g')/
  endif
  execute "sil! keepj '<,'>sort i"
  execute "sil! keepj ".firstline.",".lastline.'s/^.\{-}@@@//'
  let @a = keeprega
endfun

" VSetSearch - Search for selected text, forwards or backwards. {{{2
function! s:VSetSearch(cmdtype) abort
  let temp = @s
  norm! gv"sy
  let @/ = '\V' . substitute(escape(@s, a:cmdtype.'\'), '\n', '\\n', 'g')
  let @s = temp
endfunction

" FOR STATUS LINE {{{2
" ALE_Status {{{3
function! ALE_Status() abort
  if exists('g:loaded_ale_dont_use_this_in_other_plugins_please')
    let l:counts = ale#statusline#Count(bufnr(''))
    let l:all_errors = l:counts.error + l:counts.style_error
    let l:all_non_errors = l:counts.total - l:all_errors
    return "*".all_errors."E ".all_non_errors."w*"
  else
    return ""
  endif
endfunction

" FileEncoding {{{3
function! FileEncoding() abort
  return (&fenc == "" ? &enc : &fenc).((exists("+bomb") && &bomb) ? " BOM" : "")
endfunction

" FileSize {{{3
function! FileSize() abort
  let bytes = getfsize(expand(@%))
  if (bytes >= 1024*1024)
    return printf('~%.1f MiB', bytes/(1024*1024.0))
  elseif (bytes >= 1024)
    return printf('~%.1f KiB', bytes/1024.0)
  elseif (bytes <= 0)
    return '0 B'
  else
    return bytes . ' B'
  endif
endfunction

" ModifBufs {{{3
function! ModifBufs() abort
  let cnt = len(filter(getbufinfo(), 'v:val.changed'))
  return cnt == 0 ? "" : ( &mod ? "[+". (cnt>1?cnt:"") ."]" : "[".cnt."]" )
endfunction

" Number of buffers {{{3
function! NumOfBufs() abort
  let num = len(getbufinfo({'buflisted':1}))
  let hid = len(filter(getbufinfo({'buflisted':1}), 'empty(v:val.windows)'))
  return hid ? num-hid."+".hid : num
endfunction

" MAPPINGS {{{1
" Normal mode {{{2
nnoremap ' `
nnoremap '' ``
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l
nnoremap <expr> <F4> &virtualedit == "all" ? ":set virtualedit=\<CR>" : ":set virtualedit=all\<CR>"
nnoremap <F3> :set cursorline! cursorcolumn!<CR>
nnoremap <leader>= gg=G``
nnoremap <leader>b :bnext<CR>
nnoremap <leader>h :nohlsearch<CR>
nnoremap <leader>q gggqG
nnoremap <leader>r :%s/\<<C-r><C-w>\>//g<Left><Left>
nnoremap <leader>R :%s/\<<C-r><C-w>\>/<C-r><C-w>/g<Left><Left>
nnoremap <M-A> :copen<CR>
nnoremap <silent> <expr> <F5> g:colors_name == "darkness" ? ":syntax reset <bar> let g:colors_name=''\<CR>" : ":colo darkness\<CR>"
nnoremap gx :execute 'silent! !xdg-open "<cfile>" > /dev/null 2>&1 &' <bar> redraw! <CR>
nnoremap j gj
nnoremap k gk

" Terminal mode {{{2
tnoremap <C-h> <C-\><C-n><C-w>h
tnoremap <C-j> <C-\><C-n><C-w>j
tnoremap <C-k> <C-\><C-n><C-w>k
tnoremap <C-l> <C-\><C-n><C-w>l

" Visual mode {{{2
xnoremap # :<C-u>call <SID>VSetSearch('?')<CR>?<C-R>=@/<CR><CR>
xnoremap * :<C-u>call <SID>VSetSearch('/')<CR>/<C-R>=@/<CR><CR>

" ### DISABLE {{{2
map gh <nop>
vmap s <nop>

" OPTIONS {{{1

let g:netrw_dirhistmax = 0
let g:netrw_winsize = -28

set backup
set colorcolumn=+1,+21
set completeopt=menuone,noinsert,noselect
set hidden
set hlsearch
set ignorecase
set incsearch
set laststatus=2
set linebreak
set modeline
set nomodelineexpr
set nowrap
set number
set omnifunc=syntaxcomplete#Complete
set scrolloff=5
set shortmess+=I
set showcmd
set smartcase
set splitbelow
set splitright
set switchbuf=useopen
set t_TE= t_TI= " Disable modifyOtherKeys
set title
set undofile
set wildmenu
set wildmode=full

if has('nvim')
  set guicursor=n-v-c-sm-i-r-ci-ve-cr-o:block " setting a:block breaks blinking
else
  set sessionoptions=blank,buffers,folds,tabpages,winsize,terminal
endif

let g:sh_fold_enabled=5
set foldmethod=indent
set nofoldenable

set dictionary+=/usr/share/dict/polish
set dictionary+=/usr/share/dict/words
set path=**,./
set tags+=.git/tags;

" PLUGINS {{{1

if empty(glob(substitute(&packpath, ",.*", "/pack/plugins/opt/minPlug", ""))) " {{{
  call system("git clone --depth=1 https://github.com/Jorengarenar/minPlug ".substitute(&packpath, ",.*", "/pack/plugins/opt/minPlug", ""))
  autocmd VimEnter * silent! MinPlugInstall | echo "minPlug: INSTALLED"
endif " }}}

packadd minPlug

MinPlug ctrlpvim/ctrlp.vim
MinPlug dense-analysis/ale
MinPlug Jorengarenar/fauxClip
MinPlug Jorengarenar/vim-darkness
MinPlug mbbill/undotree
MinPlug sirver/ultisnips
MinPlug tpope/vim-surround

MinPlug! artur-shaik/vim-javacomplete2
MinPlug! vim-scripts/dbext.vim

" MAPPINGS
inoremap <silent> <C-x><Tab> <C-r>=UltiSnips_complete()<CR>
nmap <M-a> <Plug>(ale_detail)
nmap s ys
nnoremap <F1> :UndotreeToggle<cr>
nnoremap <F2> :call ALE_toggle_highlight()<CR>
vmap s S

" OPTIONS
let g:ale_set_loclist  = 0
let g:ale_set_quickfix = 1
let g:ale_set_signs    = 0

highlight ALEError   cterm=NONE
highlight ALEWarning cterm=NONE

" STATUS LINE {{{1

set statusline=
set statusline+={%{NumOfBufs()}}       " Number of buffers
set statusline+=\ \                    " Separator
set statusline+=%f                     " Relative path to the file
set statusline+=\ \                    " Separator
set statusline+=%y                     " Filetype
set statusline+=[%{&ff}]               " File format
set statusline+=[%{FileEncoding()}]    " File encoding
set statusline+=\ \                    " Separator
set statusline+=[%{&fo}]               " Format options
set statusline+=\ \                    " Separator
set statusline+=[%{FileSize()}]        " File size
set statusline+=\ \                    " Separator
set statusline+=%r                     " Readonly flag
set statusline+=%{&ma\|\|&ro?'':'[-]'} " No modifiable flag
set statusline+=%w                     " Preview flag
set statusline+=\ \                    " Separator
set statusline+=%{ModifBufs()}         " Modified flag (extended)
set statusline+=%=                     " Switch to the right side
set statusline+=%{ALE_Status()}        " ALE ststus
set statusline+=\ \                    " Separator
set statusline+=<0x%02B>               " Current char hex code
set statusline+=\ \                    " Separator
set statusline+=%l/                    " Current line
set statusline+=%L                     " Total lines
set statusline+=\ \:\ %c\              " Current column

" ### OTHER {{{1

augroup OTHER " {{{
  autocmd!

  " Open file at the last known position
  autocmd BufReadPost * if line("'\"") >= 1 && line("'\"") <= line("$") && &ft !~# 'commit' | exe "normal! g`\"" | endif

  " Trim trailing whitespace
  autocmd BufWritePre * silent! undojoin | %s/\s\+$//e | %s/\(\n\r\?\)\+\%$//e

  " Set 'foldmethod' to 'syntax'
  autocmd filetype css,html,javascript,sh,typescript setlocal foldmethod=syntax

  " Protect folds when toggling insert mode / switching windows
  autocmd InsertEnter * if !exists('w:last_fdm') | let w:last_fdm=&foldmethod | setlocal foldmethod=manual | endif
  autocmd InsertLeave,WinLeave * if exists('w:last_fdm') | let &l:foldmethod=w:last_fdm | unlet w:last_fdm | endif

  " Automatically open QuickFix
  autocmd QuickFixCmdPost [^l]* nested cwindow
  autocmd QuickFixCmdPost    l* nested lwindow

  " Terminal options
  if !has('nvim')
    autocmd TerminalWinOpen * setlocal nonumber | autocmd WinEnter <buffer> silent! normal i
  endif

augroup END " }}}

call mkdir(&backupdir, 'p')
call mkdir($XDG_DATA_HOME."/vim/spell", 'p')

packadd cfilter
packadd matchit
packadd termdebug
runtime ftplugin/man.vim

" vim: fdm=marker fen:
