#!/usr/bin/env expect

if {! [info exists ::env(TELNET_EXP_SSH)]} {
    send_user -- "TELNET_EXP_SSH env variable not set\n"
    exit
}

set rc $::env(TELNET_EXP_SSH)

set host [lindex $argv 0]

if {$host eq ""} {
    exit
}

set conf [exec ssh -F $rc -G $host]

set hostname ""
set port ""
set user_prompt "Login:"
set user ""
set pass_prompt "Password:"
set pass_cmd ""

proc set_var { var line } {
    set ::$var [lrange [split $line] 1 end]
}

foreach line [split $conf \n] {
    switch -glob -- $line {
        "proxycommand *" {
            set is_telnet [lrange [split $line] 1 end]
        }

        "hostname *" {
            set_var hostname $line
        }

        "port *" {
            set_var port $line
        }

        "user *" {
            set_var user $line
        }

        "controlpath *" {
            set_var user_prompt $line
        }

        "bindinterface *" {
            set_var pass_prompt $line
        }

        "bindaddress *" {
            set_var pass_cmd $line
        }
    }
}


if {$pass_cmd eq ""} {
    send_user -- "no pass_cmd\n"
    exit
}

set pass [exec sh -c "$pass_cmd"]
if {$pass eq ""} {
    send_user -- "no pass\n"
    exit
}

spawn telnet $hostname $port

expect {
    "$user_prompt" {
        send -- "$user\r"
        exp_continue
    }
    "$pass_prompt" {
        send -- "$pass"
        send -- "\r"
    }
}

interact
